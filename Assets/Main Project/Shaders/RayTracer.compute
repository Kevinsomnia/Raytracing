// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel Raytracer

static const float MAX_DIST = 100000.0;
static const float PI = 3.14159265;
static const float E = 2.71828183;

// Ray structs.
struct Ray {
	float3 origin;
	float3 direction;
	float3 lightEnergy;
};

struct RayHit {
	float3 position;
	float3 normal;
	float distance;
	float3 albedo;
	float3 specular;
	float smoothness;
};

// Light structs.
struct PointLight {
	float3 position;
	float radius;
	float4 color;
};

// Renderer structs.
struct Sphere {
	float3 position;
	float radius;
	float3 albedo;
	float3 specular;
	float smoothness;
};

// Rendering.
RWTexture2D<float4> _Buffer;
TextureCube<float4> _Skybox;
SamplerState sampler_Skybox;
int _MaxBounces;
float3 _AmbientColor;
float4 _FogParams;
float3 _LightDirection;
float4 _LightColor;
float3 _CameraPosition;
float4x4 _CameraToWorld;
float4x4 _CameraInvProjection;
bool _HighQuality;
float _NoiseTime;
float2 _NoisePixel;

// Light instances.
StructuredBuffer<PointLight> _PointLights;

// Renderer instances.
StructuredBuffer<Sphere> _SphereRenderers;

Ray CreateRay(float3 origin, float3 direction) {
	Ray r;
	r.origin = origin;
	r.direction = normalize(direction);
	r.lightEnergy = float3(1.0, 1.0, 1.0);
	return r;
}

Ray CreateCameraRay(float2 uv) {
	float3 direction = mul(_CameraInvProjection, float4(uv, 0.0, 1.0)).xyz;
	direction = mul(_CameraToWorld, float4(direction, 0.0)).xyz;
	return CreateRay(_CameraPosition, direction);
}

RayHit CreateRayHit() {
	RayHit hit;
	hit.distance = MAX_DIST;
	return hit;
}

void IntersectGroundPlane(Ray r, inout RayHit result) {
	// Calculate distance to ground plane.
	float dist = -r.origin.y / r.direction.y;

	if (r.origin.y > 0.0 && dist > 0.0 && dist < result.distance) {
		result.position = r.origin + (r.direction * dist);
		result.normal = float3(0.0, 1.0, 0.0); // Facing up.
		result.distance = dist;
		result.albedo = float3(0.25, 0.25, 0.25);
		result.specular = float3(0.15, 0.15, 0.15);
		result.smoothness = 0.85;
	}
}

void IntersectSphere(Ray r, Sphere s, inout RayHit result) {
	// Calculate distance to sphere surface.
	// https://en.wikipedia.org/wiki/Line-sphere_intersection
	float3 dir = r.origin - s.position;
	float dirDot = -dot(r.direction, dir);
	float rSquared = s.radius * s.radius;
	float quadraticDist = (dirDot * dirDot) - (dot(dir, dir) - rSquared);
	
	if (quadraticDist < 0.0)
		return; // No solutions exist.

	// Get the closer candidate for distance.
	quadraticDist = sqrt(quadraticDist);
	float d1 = dirDot - quadraticDist;
	float d2 = dirDot + quadraticDist;
	d1 = (d1 < d2) ? d1 : d2;

	if (d1 > 0.0 && d1 < result.distance) {
		result.position = r.origin + (r.direction * d1);
		result.normal = normalize(result.position - s.position); // Facing towards hit position from sphere center.
		result.distance = d1;
		result.albedo = s.albedo;
		result.specular = s.specular;
		result.smoothness = s.smoothness;
	}
}

RayHit Raytrace(Ray ray) {
	RayHit result = CreateRayHit();
	IntersectGroundPlane(ray, result);

	// Render spheres.
	uint sphereCount;
	uint sphereStride;
	_SphereRenderers.GetDimensions(sphereCount, sphereStride);

	for (uint i = 0; i < sphereCount; i++) {
		IntersectSphere(ray, _SphereRenderers[i], result);
	}

	return result;
}

float Noise() {
	// The infamous noise function.
	float n = frac(sin(_NoiseTime + dot(_NoisePixel, float2(12.9898, 78.233))) * 43758.5453);
	return n;
}

float Luminosity(float3 color) {
	// Relative luminance.
	return dot(color, float3(0.2126, 0.7152, 0.0722));
}

float SaturateDot(float3 a, float3 b, float factor) {
	return saturate(dot(a, b) * factor);
}

float3x3 GetTangentSpace(float3 normal) {
	float3 crossFwd = float3(1.0, 0.0, 0.0);

	if (abs(normal.x) > 0.99) {
		crossFwd = float3(0.0, 0.0, 1.0);
	}

	float3 tangent = normalize(cross(normal, crossFwd));
	float3 binormal = normalize(cross(normal, tangent));
	return float3x3(tangent, binormal, normal);
}

float3 SampleHemisphere(float3 normal, float alpha) {
	float cosTheta = pow(Noise(), 1.0 / alpha);
	float sinTheta = sqrt(max(0.0, 1.0 - (cosTheta * cosTheta)));
	float phi = 2.0 * PI * Noise();
	float3 tangentSpaceDir = float3(cos(phi) * sinTheta, sin(phi) * sinTheta, cosTheta);

	return mul(tangentSpaceDir, GetTangentSpace(normal));
}

void ApplyFogColor(inout float3 col, float dist) {
	float fogFactor = 1.0 - pow(E, -_FogParams.w * dist);
	col = lerp(col, _FogParams.rgb, fogFactor);
}

void ApplyPointLight(inout float3 col, PointLight light, Ray ray, RayHit hit) {
	// Direction from point light position to sampled position.
	float3 lightDir = (ray.origin - light.position);
	float lightDist = length(lightDir);
	lightDir = normalize(lightDir);

	if (lightDist < light.radius) {
		// Exclude shadow calculation for areas facing away from the light source.
		// Will also need this value to handle light attenuation.
		float nDotL = saturate(-dot(hit.normal, lightDir));

		if (nDotL > 0.0) {
			// Within point light radius and facing light source. Test for shadows by casting ray towards point light center.
			Ray shadowRay = CreateRay(ray.origin, -lightDir);
			RayHit shadowHit = Raytrace(shadowRay);

			if (shadowHit.distance > lightDist) {
				// This spot is NOT a shadow (ray was able to pass through light's center).
				// Add the point light color with attenuation and nDotL.
				float falloff = (light.radius - lightDist) / light.radius;
				col += hit.albedo * light.color.rgb * light.color.a * falloff * falloff * nDotL;
			}
		}
	}
}

float SmoothnessToPhongAlpha(float smoothness) {
	return pow(1000.0, smoothness * smoothness);
}

float3 GetRayColor(inout Ray ray, RayHit hit) {
	if (hit.distance < MAX_DIST) {
		// Reflect ray off surface normal and diminish energy.
		ray.origin = hit.position + (hit.normal * 0.001);
		float3 rayReflection = reflect(ray.direction, hit.normal);

		if (_HighQuality) {
			// Physical shading by limiting albedo shading by specularity.
			hit.albedo = min(1.0 - hit.specular, hit.albedo);

			// Calculate distribution of rays.
			float diffuseChance = Luminosity(hit.albedo);
			float specularChance = Luminosity(hit.specular);
			// Normalize both values to add to 1.0.
			float totalChance = diffuseChance + specularChance;
			diffuseChance /= totalChance;
			specularChance = 1.0 - diffuseChance;

			float raySelector = Noise();

			if (raySelector < diffuseChance) {
				// Diffuse reflections.
				ray.direction = SampleHemisphere(hit.normal, 2.0);
				ray.lightEnergy *= (2.0 * SaturateDot(ray.direction, hit.normal, 1.0) / diffuseChance) * hit.albedo;
			}
			else {
				// Specular (Phong) reflections.
				float alpha = SmoothnessToPhongAlpha(hit.smoothness);
				ray.direction = SampleHemisphere(rayReflection, alpha);
				float f = (alpha + 2.0) / (alpha + 1.0);
				ray.lightEnergy *= (SaturateDot(ray.direction, hit.normal, f) / specularChance) * hit.specular;
			}
		}
		else {
			ray.direction = rayReflection;
			ray.lightEnergy *= hit.specular;
		}

		// DIRECTIONAL LIGHT.
		// Get albedo color of surface, multiplied with nDotL and light color.
		float3 finalColor = hit.albedo * _LightColor.rgb * _LightColor.a;
		float nDotL = saturate(-dot(hit.normal, _LightDirection.xyz));

		if (nDotL > 0.0) {
			// Shadow test. Shoot ray towards light source(s) to determine if there's anything in between light and this point.
			Ray shadowRay = CreateRay(ray.origin, -_LightDirection.xyz);
			RayHit shadowHit = Raytrace(shadowRay);

			if (shadowHit.distance < MAX_DIST) {
				// Within shadow. Apply ambient light / shadow color.
				finalColor = _AmbientColor;
			}
			else {
				// nDotL falloff.
				finalColor = lerp(_AmbientColor, finalColor, nDotL);
			}
		}
		else {
			finalColor = _AmbientColor; // Pointing away from directional light.
		}

		// POINT LIGHTS.
		uint pointLightCount;
		uint pointLightStride;
		_PointLights.GetDimensions(pointLightCount, pointLightStride);

		for (uint i = 0; i < pointLightCount; i++) {
			ApplyPointLight(finalColor, _PointLights[i], ray, hit);
		}

		return finalColor;
	}
	else {
		// Ray hit nothing. Render skybox.
		ray.lightEnergy = 0.0;
		return _Skybox.SampleLevel(sampler_Skybox, ray.direction, 0).rgb;
	}
}

[numthreads(16,16,1)]
void Raytracer(uint3 id : SV_DispatchThreadID) {
	// Set noise parameters.
	_NoisePixel = id.xy;

	// Get texture dimensions.
	uint w;
	uint h;
	_Buffer.GetDimensions(w, h);

	// Transform UV to world ray.
	float2 uv =	((id.xy / float2(w, h)) - 0.5) * 2.0;
	Ray camRay = CreateCameraRay(uv);

	float3 finalColor = float3(0.0, 0.0, 0.0);
	float3 finalPos = _CameraPosition;

	for (int i = 0; i < _MaxBounces; i++) {
		RayHit raytrace = Raytrace(camRay);
		finalColor += camRay.lightEnergy * GetRayColor(camRay, raytrace);
		finalPos = raytrace.position;

		float energySum = camRay.lightEnergy.r + camRay.lightEnergy.g + camRay.lightEnergy.b;

		if (energySum < 0.001)
			break; // No energy left.
	}

	// Apply fog color with squared falloff.
	ApplyFogColor(finalColor, distance(_CameraPosition, finalPos));

	_Buffer[id.xy] = float4(finalColor, 1.0);
}